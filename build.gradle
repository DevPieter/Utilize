plugins {
    id 'fabric-loom'
    id 'com.replaymod.preprocess'
    id 'maven-publish'
}

def targetJavaVersion = 21

def isSnapshot = providers.environmentVariable("SNAPSHOT_BUILD").map { it.toBoolean() }.orElse(false).get()
def buildNumber = System.getenv('BUILD_NUMBER') ?: 'dev'

group = maven_group
version = "${mod_version}+${minecraft_version}-${max_minecraft_version}" + (isSnapshot ? "-SNAPSHOT+${buildNumber}" : "")

base {
    archivesName = archives_base_name
}

repositories {
    maven {
        name "Pietr Space Releases"
        url "https://maven.pietr.space/releases"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    implementation "nl.devpieter:sees:1.2.1"
    include "nl.devpieter:sees:1.2.1"
}

preprocess {
    vars.put("MC", project.mcVersion)
    vars.put("FABRIC", 1)
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "max_minecraft_version", project.max_minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "max_minecraft_version": project.max_minecraft_version,
                "loader_version": project.loader_version
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    withSourcesJar()
    withJavadocJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = artifact_id
        }
    }
    repositories {
        maven {
            url = uri(providers.gradleProperty("mavenRepositoryUrl").orNull ?: "http://localhost:8081/releases/nl/devpieter/utilize")
            credentials {
                username = providers.gradleProperty("mavenRepositoryUsername").orNull
                password = providers.gradleProperty("mavenRepositoryPassword").orNull
            }
        }
    }
}
